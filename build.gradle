buildscript {
    ext.kotlin_version = '1.8.10'
    if (!project.hasProperty('version') || project.version.equals('unspecified')) {
        project.version = '+'
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.1'
        classpath 'com.mparticle:android-kit-plugin:' + project.version
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jlleitschuh.gradle.ktlint" version "11.2.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "mparticle-android-integration-revealmobile"
        property "sonar.organization", "mparticle"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.mparticle.kit'
apply plugin: 'kotlin-android'

android {
    namespace 'com.mparticle.kits.revealmobile'
    compileSdk 33
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 16
        targetSdk 33
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // Changed from 'api' to 'compileOnly' to avoid invalid POM issues
    compileOnly files('libs/reveal-1.4.22-android12.aar')

    // If ever published as a proper Maven artifact, you can switch to:
    // api 'com.revealmobile:reveal:1.4.22'
}

// âœ¨ Remove invalid dependency blocks from the published POM
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom.withXml {
                    def dependenciesNode = asNode().get('dependencies')
                    dependenciesNode?.removeAll {
                        def groupId = it.getAt('groupId')[0]?.text()
                        return groupId == null || groupId.trim() == ''
                    }
                }
            }
        }
    }
}